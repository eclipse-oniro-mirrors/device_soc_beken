# Copyright (C) 2022 Beken Corporation
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//kernel/liteos_m/liteos.gni")
import("$LITEOSTHIRDPARTY/lwip/lwip.gni")
import("$LITEOSTOPDIR/components/net/lwip-2.1/lwip_porting.gni")
import("${beken_sdk_dir}/sdkconfig.gni")
import("${board_adapter_dir}/hals/sdk_dir.gni")

kernel_module("lwip") {
  sources = LWIPNOAPPSFILES + MDNSFILES - PPPFILES - SIXLOWPAN - [
              "$LWIPDIR/api/sockets.c",
              "$LWIPDIR/api/if_api.c",
              "$LWIPDIR/core/altcp.c",
              "$LWIPDIR/core/altcp_alloc.c",
              "$LWIPDIR/core/altcp_tcp.c",
              "$LWIPDIR/netif/bridgeif.c",
              "$LWIPDIR/netif/bridgeif_fdb.c",
              "$LWIPDIR/netif/slipif.c",
            ]
  sources += [
    "dhcpd/dhcp-server-main.c",
    "dhcpd/dhcp-server.c",
    "lwip-2.1.2/port/ethernetif.c",
    "lwip-2.1.2/port/net.c",
    "lwip-2.1.2/port/sockets.c",
    "lwip-2.1.2/port/sys_arch.c",
  ]

  include_dirs = []
  deps = []
  public_configs = [
    ":public",
    "${beken_sdk_dir}/components/bk_os:public",
    "${beken_sdk_dir}/components/bk_wifi:public",
    "${beken_sdk_dir}/middleware/driver:public",
  ]
}

config("public") {
  include_dirs = [
    "lwip-2.1.2/port",
    "$LWIPDIR",
    "$LWIPDIR/include",
    "$LWIPDIR/include/netif",
    "$LWIPDIR/include/lwip",
    "$LITEOSTOPDIR/arch/risc-v/riscv32/gcc",
    "$LITEOSTOPDIR/arch/risc-v/riscv32/gcc/asm",
    "$LITEOSTOPDIR/arch/include",
    "$LITEOSTOPDIR/kernel/include",
    "$LITEOSTOPDIR/kal/cmsis",
    "$LITEOSTOPDIR/kal/posix/include",
    "$LITEOSTOPDIR/components/power",
    "$LITEOSTOPDIR/components/signal",
    "$LITEOSTOPDIR/utils",
    "$extra_include_dir",
  ]
}
